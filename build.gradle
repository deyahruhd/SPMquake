buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

minecraft {
    version = "1.12.2-14.23.0.2486"
    mappings = "stable_39"
    runDir = "run"

    coreMod = "squeek.quakemovement.asm.ASMPlugin"

    replace "\${version}", project.version
    replaceIn "ModInfo.java"
}

group = project.projectDir.name.toLowerCase()
archivesBaseName = project.projectDir.name + "-mc" + project.minecraft.version

sourceSets.main.java.srcDirs += 'java'
sourceSets.main.resources.srcDirs += 'resources'

jar {
    manifest {
        attributes 'FMLCorePlugin': 'squeek.quakemovement.asm.ASMPlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

processResources {
    inputs.property "vars", project.version + project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}
